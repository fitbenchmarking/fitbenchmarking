***************************
* SET UP THE INITIAL DATA *
***************************

NAME          MGH10

*   Problem :
*   *********

*   NIST Data fitting problem MGH10 given as an inconsistent set of
*   nonlinear equations.

*   Fit: y = b1 * exp[b2/(x+b3)] + e

*   Source:  Problem from the NIST nonlinear regression test set
*     http://www.itl.nist.gov/div898/strd/nls/nls_main.shtml

*   Reference: Meyer, R. R. (1970).  
*     Theoretical and computational aspects of nonlinear 
*     regression.  In Nonlinear Programming, Rosen, 
*     Mangasarian and Ritter (Eds).  
*     New York, NY: Academic Press, pp. 465-486.

*   SIF input: Nick Gould and Tyrone Rees, Oct 2015

*   classification NOR2-MN-3-16

*   Number of data values

 IE M                   16

*   Number of variables

 IE N                   3

*   Useful parameters

 IE 1                   1

*  Data values

 RE X1                  5.00E+01
 RE X2                  5.50E+01
 RE X3                  6.00E+01
 RE X4                  6.50E+01
 RE X5                  7.00E+01
 RE X6                  7.50E+01
 RE X7                  8.00E+01
 RE X8                  8.50E+01
 RE X9                  9.00E+01
 RE X10                 9.50E+01
 RE X11                 1.00E+02
 RE X12                 1.05E+02
 RE X13                 1.10E+02
 RE X14                 1.15E+02
 RE X15                 1.20E+02
 RE X16                 1.25E+02

 RE Y1                  3.478E+04
 RE Y2                  2.861E+04
 RE Y3                  2.365E+04
 RE Y4                  1.963E+04
 RE Y5                  1.637E+04
 RE Y6                  1.372E+04
 RE Y7                  1.154E+04
 RE Y8                  9.744E+03
 RE Y9                  8.261E+03
 RE Y10                 7.030E+03
 RE Y11                 6.005E+03
 RE Y12                 5.147E+03
 RE Y13                 4.427E+03
 RE Y14                 3.820E+03
 RE Y15                 3.307E+03
 RE Y16                 2.872E+03

VARIABLES

 DO I         1                        N
 X  B(I)
 ND

GROUPS

 DO I         1                        M
 XE F(I)
 ND

CONSTANTS

 DO I         1                        M
 Z  MGH10     F(I)                     Y(I)
 ND        

BOUNDS

 LO BND       B1        0.0
 UP BND       B1        1.0
 LO BND       B2        100.0
 UP BND       B2        10000.0
 LO BND       B3        100.0
 UP BND       B3        500.0

START POINT

    START2    B1        0.02  
    START2    B2        4000.0
    START2    B3        250.0 

ELEMENT TYPE

 EV E12       V1
 EV E12       V2
 EV E12       V3
 EP E12       X

ELEMENT USES

 DO I         1                        M
 XT E(I)      E12
 ZV E(I)      V1                       B1
 ZV E(I)      V2                       B2
 ZV E(I)      V3                       B3
 ZP E(I)      X                        X(I)
 ND

GROUP USES

 DO I         1                        M
 XE F(I)      E(I)
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO MGH10               0.0

*   Solution

*LO SOLTN               

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      MGH10

TEMPORARIES

 R  E
 R  F
 R  V3PX
 R  V3PX2
 R  V3PX3
 M  EXP

INDIVIDUALS

 T  E12
 A  V3PX                V3 + X
 A  V3PX2               V3PX * V3PX
 A  V3PX3               V3PX * V3PX2
 A  E                   EXP( V2 / V3PX )
 A  F                   V1 * E
 F                      F
 G  V1                  E
 G  V2                  F / V3PX
 G  V3                  - F * V2 / V3PX2
 H  V1        V2        E / V3PX
 H  V1        V3        - V2 * E / V3PX2
 H  V2        V2        F / V3PX2
 H  V2        V3        - F * ( 1.0 / V3PX2 + V2 / V3PX3 )
 H  V3        V3        F * V2 * ( V2 / V3PX ** 4 
 H+                                + 2.0 / V3PX3 )

ENDATA
